mapscripts SummertreeThicket_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: SummertreeThicket_DefaultScript_DefaultState
}

script SummertreeThicket_DefaultScript_DefaultState{
    setflag(FLAG_SUMMERTREE_CUTSCENE)
}

script SummertreeThicket_MusicScript_GruntEncounter{
    playbgm(MUS_ENCOUNTER_AQUA, FALSE)
    return
}

script SummertreeThicket_TriggerScript_Cutscene1{
    lock
    addobject(9)
    addobject(10)
    addobject(11)
    applymovement(OBJ_EVENT_ID_PLAYER, SummertreeThicket_MovementScript_PlayerToCutscene1)
    waitmovement(0)
    applymovement(9, SummertreeThicket_MovementScript_RivalToPlayerFirst)
    waitmovement(0)
    call(SummertreeThicket_SequenceScript_PreBattleDialogue)
    setvar(VAR_ROUTE_3_CUTSCENE, 3)
    release
    end
}

script SummertreeThicket_TriggerScript_Cutscene2{
    lock
    addobject(9)
    addobject(10)
    addobject(11)
    applymovement(OBJ_EVENT_ID_PLAYER, SummertreeThicket_MovementScript_PlayerToCutscene2)
    waitmovement(0)
    applymovement(9, SummertreeThicket_MovementScript_RivalToPlayerFirst)
    waitmovement(0)
    call(SummertreeThicket_SequenceScript_PreBattleDialogue)
    setvar(VAR_ROUTE_3_CUTSCENE, 3)
    release
    end
}

script SummertreeThicket_SequenceScript_PreBattleDialogue{
    msgbox(SummertreeThicket_TextScript_RivalDialogue1)
    special(HealPlayerParty)
    applymovement(9, SummertreeThicket_MovementScript_RivalToGrunt)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, SummertreeThicket_MovementScript_PlayerToGrunt)
    waitmovement(0)
    applymovement(10, SummertreeThicket_MovementScript_GruntToPlayer)
    waitmovement(0)
    call(SummertreeThicket_TrainerScript_FrostGrunt)
    applymovement(10, SummertreeThicket_MovementScript_GruntLeaveFirst)
    waitmovement(0)
    removeobject(10)
    playse(SE_EXIT)
    applymovement(11, SummertreeThicket_MovementScript_GruntLeaveSecond)
    waitmovement(0)
    removeobject(11)
    playse(SE_EXIT)
    fadedefaultbgm
    call(SummertreeThicket_SequenceScript_PostBattleDialogue)
    return
}

script SummertreeThicket_SequenceScript_PostBattleDialogue{
    applymovement(OBJ_EVENT_ID_PLAYER, SummertreeThicket_MovementScript_PlayerToRival)
    waitmovement(0)
    applymovement(9, SummertreeThicket_MovementScript_RivalToPlayerFirst)
    waitmovement(0)
    msgbox(format("Thanks for helping me with that {PLAYER}. It sucks that they fled before we could get the authorities involved. One thing we can do is in the next town over, I head there's a police station that we could report to. The gym leader there is a part of it, so we could get some much needed help. Well then, see you later."))
    applymovement(9, SummertreeThicket_MovementScript_RivalLeave)
    waitmovement(0)
    removeobject(9)
    playse(SE_EXIT)
    return
}

script SummertreeThicket_SignScript_ForestSign{
    lock
    msgbox(format("Summertree Wood, a bountiful forest."))
    release
    end
}

script SummertreeThicket_ItemScript_BallOne{
    lock
    giveitem(ITEM_SUPER_POTION, 1)
    //goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
    removeobject(3)
    release
    end
}

script SummertreeThicket_ItemScript_BallTwo{
    lock
    giveitem(ITEM_ANTIDOTE, 1)
    removeobject(5)
    release
    end
}

script SummertreeThicket_ItemScript_BallThree{
    lock
    giveitem(ITEM_MIRACLE_SEED, 1)
    removeobject(2)
    release
    end
}

script SummertreeThicket_TrainerScript_FrostGrunt{
    call(SummertreeThicket_MusicScript_GruntEncounter)
    msgbox(SummertreeThicket_TextScript_GruntIntro)
    trainerbattle_no_intro(TRAINER_SUMMERTREE_FROST_GRUNT, SummertreeThicket_TextScript_GruntOutro)
    msgbox(SummertreeThicket_TextScript_AfterBattleGruntDialogue)
    closemessage
    return
}

script SummertreeThicket_TrainerScript_Caleb{
    trainerbattle_single(TRAINER_CALEB_1, SummertreeThicket_TextScript_CalebIntro, SummertreeThicket_TextScript_CalebOutro)
    msgbox(format("If you're looking for a good bug type Pokemon, this is the place to look."), MSGBOX_AUTOCLOSE)
    end
}

script SummertreeThicket_TrainerScript_Lizzy{
    trainerbattle_single(TRAINER_LIZZY_1, SummertreeThicket_TextScript_LizzyIntro, SummertreeThicket_TextScript_LizzyOutro)
    msgbox(format("You should picnick here sometime. Eating good food in this atmosphere is the best."), MSGBOX_AUTOCLOSE)
    end
}

script SummertreeThicket_TrainerScript_Joshua{
    trainerbattle_single(TRAINER_JOSHUA_1, SummertreeThicket_TextScript_JoshuaIntro, SummertreeThicket_TextScript_JoshuaOutro)
    msgbox(format("If you ever decide to go camping, consider this forest as your primetime destination."), MSGBOX_AUTOCLOSE)
    end
}

script SummertreeThicket_TrainerScript_Tina{
    trainerbattle_single(TRAINER_TINA_1, SummertreeThicket_TextScript_TinaIntro, SummertreeThicket_TextScript_TinaOutro)
    msgbox(format("Any luck in finding some good Pokemon?"), MSGBOX_AUTOCLOSE)
    end
}

movement SummertreeThicket_MovementScript_PlayerToCutscene1{
    walk_left * 4
    walk_down
    walk_left
    walk_down 
    walk_left * 3
}

movement SummertreeThicket_MovementScript_PlayerToCutscene2{
    walk_left * 5
    walk_down
    walk_left * 3
}

movement SummertreeThicket_MovementScript_RivalToPlayerFirst{
    face_right
}

movement SummertreeThicket_MovementScript_RivalToGrunt{
    face_left
}

movement SummertreeThicket_MovementScript_PlayerToGrunt{
    walk_up
    face_left
}

movement SummertreeThicket_MovementScript_GruntToPlayer{
    face_right
}

movement SummertreeThicket_MovementScript_GruntLeaveFirst{
    walk_fast_up
}

movement SummertreeThicket_MovementScript_GruntLeaveSecond{
    walk_fast_up * 2
}

movement SummertreeThicket_MovementScript_RivalLeave{
    walk_up * 2
}

movement SummertreeThicket_MovementScript_PlayerToRival{
    walk_down
    face_left
}

text SummertreeThicket_TextScript_AfterBattleGruntDialogue{
    format("Curse you, I lost! Even my partner has lost to your friend!? Sigh, we should leave quickly before you call the authorities and have us arrested. Remember that next time, you'll regret crossing us!")
}

text SummertreeThicket_TextScript_GruntIntro{
    format("That kid shouldn't have gotten into trouble with us if he wanted to be safe. I see you're making the same mistake. I'll show you the folly of your decision!")
}

text SummertreeThicket_TextScript_GruntOutro{
    format("What the hell are you!?")
}

text SummertreeThicket_TextScript_RivalDialogue1{
    format("Red: Great timing {PLAYER}! I found these thugs lurking around, acting all suspicious. Then, when I asked what they were up to, they ambushed me! Luckily you pulled up when you did or I would be in deep trouble. Please fend off the thug above me while I fight the other. Let me heal your Pokemon real quick while I have this woman occupied. Be safe and good luck!")
}

text SummertreeThicket_TextScript_CalebIntro{
    format("This forest is a utopia for bug Pokemon! Let's have a battle in celebration of this amazing place!")
}

text SummertreeThicket_TextScript_CalebOutro{
    format("You are one great trainer...")
}

text SummertreeThicket_TextScript_LizzyIntro{
    format("I love picnicking in these woods. It's so serene here!")
}

text SummertreeThicket_TextScript_LizzyOutro{
    format("Be safe exploring the forest. Thanks for the battle.")
}

text SummertreeThicket_TextScript_JoshuaIntro{
    format("Camping in this forest is really fun. I'm in the perfect mood for a battle!")
}

text SummertreeThicket_TextScript_JoshuaOutro{
    format("I can't be mad. You did great!")
}

text SummertreeThicket_TextScript_TinaIntro{
    format("There's some interesting Pokemon that live in this forest. I love places like this.")
}

text SummertreeThicket_TextScript_TinaOutro{
    format("I wish you luck on finding some good Pokemon to raise!")
}
