mapscripts GloriaTown_Rival_House_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: GloriaTown_Rival_House_DefaultScript_DefaultState

    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_RIVAL_HOUSE_STATE, 0: GloriaTown_Rival_House_SceneScript_GrandmaDialogueScene
    ]
}

script GloriaTown_Rival_House_DefaultScript_DefaultState{
    if(!(var(VAR_RIVAL_HOUSE_STATE))){
        setvar(VAR_RIVAL_HOUSE_STATE, 0)
    }
}

script GloriaTown_Rival_House_PersonScript_RivalGrandma{
    lock
    faceplayer
    msgbox(format("Hello, {PLAYER}{KUN}! I'm so proud of my grandson and you for growing up so well!"))
    release
    end
}

script GloriaTown_Rival_House_SignScript_TV{
    lock
    special(TurnOnTVScreen)
    msgbox(format("It's the original Wizard of Oz! Now that's an oldie."))
    special(TurnOffTVScreen)
    release
    end
}

script GloriaTown_Rival_House_SceneScript_GrandmaDialogueScene{
    lock
    call(GloriaTown_Rival_House_ControlScript_GrandmaDialogueInit)
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Rival_House_MovementScript_ToGrandma)
    waitmovement(0)
    msgbox(GloriaTown_Rival_House_TextScript_GrandmaDialogue)
    applymovement(1, GloriaTown_Rival_House_MovementScript_GrandmaFaceTable)
    waitmovement(0)
    release
    end
}

script GloriaTown_Rival_House_ControlScript_GrandmaDialogueInit{
    setvar(VAR_RIVAL_HOUSE_STATE, 1)
    applymovement(1, GloriaTown_Rival_House_MovementScript_GrandmaFaceDoor)
    waitmovement(0)
    msgbox("Hey, {PLAYER}{KUN}!")
    closemessage
    return    
}

movement GloriaTown_Rival_House_MovementScript_GrandmaFaceDoor{
    face_left
}

movement GloriaTown_Rival_House_MovementScript_GrandmaFaceTable{
    face_right
}

movement GloriaTown_Rival_House_MovementScript_ToGrandma{
    walk_up * 2
    walk_right * 2
}

text GloriaTown_Rival_House_TextScript_GrandmaDialogue{
    format("It's nice to see you, I hope you've been doing well. My grandson has been waiting for you so please go upstairs to see him.")
}
