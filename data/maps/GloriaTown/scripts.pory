mapscripts GloriaTown_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: GloriaTown_DefaultScript_DefaultState
}

script GloriaTown_DefaultScript_DefaultState{
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        setvar(VAR_GLORIA_TOWN_STATE, 0)
    }
    
    call_if_unset(FLAG_RIVAL_BATTLE_CONTROL, GloriaTown_ControlScript_RivalBattleSetup)
}

script GloriaTown_SignScript_Player_House{
    lock
    msgbox("{PLAYER}{KUN}'s House")
    release
    end
}

script GloriaTown_SignScript_Rival_House{
    lock
    msgbox("Professor's House")
    release
    end
}

script GloriaTown_SignScript_Prof_Lab{
    lock
    msgbox("Professor's Lab")
    release
    end
}

script GloriaTown_SignScript_Gym{
    lock
    msgbox("Test Town Pokemon Gym\nLeader: Zackary\p“The wise man of iron!”")
    release
    end
}

script GloriaTown_SignScript_TownSign{
    lock
    msgbox("Gloria Town, a serene abode.")
    release
    end
}

script GloriaTown_ItemScript_BallOne{
    lock
    giveitem(ITEM_EVERSTONE, 1)
    removeobject(3)
    release
    end
}

script GloriaTown_ItemScript_BallTwo{
    lock
    giveitem(ITEM_RARE_CANDY, 1)
    removeobject(4)
    release
    end
}

script GloriaTown_PersonScript_NPC1{
    lock
    faceplayer
    msgbox(format("This town is so beautiful and calming. I'm visiting from out of town and this place is great. I might want to move here!"))
    release
    end
}

script GloriaTown_PersonScript_NPC2{
    lock
    faceplayer
    msgbox(format("I love coming to Gloria Town to fish! There's some encounters here that you can't get anywhere else. You should try it some time!"))
    release
    end
}

script GloriaTown_ControlScript_RivalBattleSetup{
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        setflag(FLAG_RIVAL_BATTLE_ONE)
    }
    else{
        if(var(VAR_GLORIA_TOWN_STATE) == 2){
            clearflag(FLAG_RIVAL_BATTLE_ONE)
        }
    }
}

script GloriaTown_TrainerScript_RivalBattleOne{
    call(GloriaTown_MusicScript_RivalEncounter)
    msgbox(GloriaTown_TextScript_RivalIntro)
    //trainerbattle_no_intro(TRAINER_RED, GloriaTown_TextScript_RivalOutro)
    trainerbattle_no_intro_no_whiteout(TRAINER_RED, GloriaTown_TextScript_RivalOutro)
    special(HealPlayerParty)
    msgbox(GloriaTown_TextScript_RivalBattleAftermath)
    closemessage
    setflag(FLAG_RIVAL_BATTLE_ONE)
    setvar(VAR_GLORIA_TOWN_STATE, 3)
    call(GloriaTown_SequenceScript_RivalLeave1)
    return
}

script GloriaTown_SequenceScript_RivalLeave1{
    lockall
    applymovement(6, GloriaTown_MovementScript_RivalLeaveAfterBattle1)
    waitmovement(0)
    removeobject(6)
    fadedefaultbgm
    releaseall
    return
}

script GloriaTown_SequenceScript_RivalLeave2{
    lockall
    applymovement(6, GloriaTown_MovementScript_RivalLeaveAfterBattle2)
    waitmovement(0)
    removeobject(6)
    fadedefaultbgm
    releaseall
    return
}

script GloriaTown_SequenceScript_RivalLeave3{
    lockall
    applymovement(6, GloriaTown_MovementScript_RivalLeaveAfterBattle3)
    waitmovement(0)
    removeobject(6)
    fadedefaultbgm
    releaseall
    return
}

script GloriaTown_MusicScript_RivalEncounter{
    playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
    return
}

script GloriaTown_TriggerScript_RivalEncounter1{
    applymovement(6, GloriaTown_MovementScript_RivalToPlayer1)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_MovementScript_FaceRival)
    waitmovement(0)
    call(GloriaTown_TrainerScript_RivalBattleOne)
    setvar(VAR_RIVAL_STATE, 3)
    end
}

script GloriaTown_TriggerScript_RivalEncounter2{
    applymovement(6, GloriaTown_MovementScript_RivalToPlayer2)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_MovementScript_FaceRival)
    waitmovement(0)
    call(GloriaTown_TrainerScript_RivalBattleOne)
    setvar(VAR_RIVAL_STATE, 3)
    end
}

script GloriaTown_TriggerScript_RivalEncounter3{
    applymovement(6, GloriaTown_MovementScript_RivalToPlayer3)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_MovementScript_FaceRival)
    waitmovement(0)
    call(GloriaTown_TrainerScript_RivalBattleOne)
    setvar(VAR_RIVAL_STATE, 3)
    end
}

script GloriaTown_TriggerScript_BlockPath{
    lockall
    msgbox("Mother said to visit the Professor and\nyour Rival before heading out!")
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_MovementScript_StopPlayer)
    waitmovement(0)
    releaseall
}

script GloriaTown_TriggerScript_GotoRival{
    lockall
    msgbox("You should go visit your rival first!")
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_MovementScript_StopPlayerLab)
    waitmovement(0)
    releaseall
}

movement GloriaTown_MovementScript_StopPlayer{
    walk_up
}

movement GloriaTown_MovementScript_StopPlayerLab{
    walk_right
}

movement GloriaTown_MovementScript_RivalToPlayer1{
    walk_down
    walk_left * 8
}

movement GloriaTown_MovementScript_RivalToPlayer2{
    walk_left * 7
}

movement GloriaTown_MovementScript_RivalToPlayer3{
    walk_left * 9
}

movement GloriaTown_MovementScript_FaceRival{
    face_right
}

movement GloriaTown_MovementScript_RivalLeaveAfterBattle1{
    walk_right * 10
}

movement GloriaTown_MovementScript_RivalLeaveAfterBattle2{
    walk_right * 9
}

movement GloriaTown_MovementScript_RivalLeaveAfterBattle3{
    walk_right * 11
}

text GloriaTown_TextScript_RivalIntro{
    format("Now that you have your Pokemon, let's battle before we both head out! I've been looking forward to this for a long time, let's get it!")
}

text GloriaTown_TextScript_RivalOutro{
    format("You are very good at this! It's surprising that this is your first battle.")
}

text GloriaTown_TextScript_RivalBattleAftermath{
    format("That was a very exhilarating battle and everything I was hoping for! Let's do this again when we get further along on our journeys. Now, let's go out and explore the world! Stay safe and see you around!")
}
