mapscripts GloriaTown_Your_House_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: GloriaTown_Your_House_DefaultScript_OnTransition
    
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_YOUR_HOUSE_STATE, 1: GloriaTown_Your_House_SceneScript_MomDialogueSceneOne
    ]
}

script GloriaTown_Your_House_DefaultScript_OnTransition{
    setrespawn(HEAL_LOCATION_GLORIA_TOWN)
    end
}

script GloriaTown_Your_House_SignScript_TV{
    lock
    special(TurnOnTVScreen)
    msgbox("Breaking Bad seems to be on, that show\nis great!")
    special(TurnOffTVScreen)
    release
    end
}

script GloriaTown_Your_House_PersonScript_Mom{
    lock
    faceplayer
    if(flag(FLAG_ADVENTURE_STARTED)){
        msgbox(format("Mom: You and your Pokemon look tired. Please take this opportunity to rest..."))
        special(HealPlayerParty)
        fadescreen(FADE_TO_BLACK)
        playfanfare(MUS_HEAL)
        delay(120)
        fadescreen(FADE_FROM_BLACK)
        msgbox(format("You all look so much better now! If you feel fatigued and are in the area, remember you are always welcome to rest here. Stay safe out there honey!"))
    }
    else{
        msgbox(format("Mom: Please go do your errands honey, they're very important."))
    }
    release
    end
}

script GloriaTown_Your_House_SceneScript_MomDialogueSceneOne{
    lock
    call(GloriaTown_Your_House_ControlScript_MomDialogueInit)
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Your_House_MovementScript_ToMom)
    waitmovement(0)
    msgbox(GloriaTown_Your_House_TextScript_MomDialogueOne)
    applymovement(1, GLORIA_TOWN_Your_House_MovementScript_MomFaceLeft)
    waitmovement(0)
    release
    end
}

script GloriaTown_Your_House_ControlScript_MomDialogueInit{
    setvar(VAR_YOUR_HOUSE_STATE, 2)
    applymovement(1, GloriaTown_Your_House_MovementScript_MomFaceRight)
    waitmovement(0)
    msgbox(format("Mom: Hello, {PLAYER}{KUN}! Please come over and see me."))
    closemessage
    return
}

movement GloriaTown_Your_House_MovementScript_MomFaceRight{
    face_right
}

movement GLORIA_TOWN_Your_House_MovementScript_MomFaceLeft{
    face_left
}

movement GloriaTown_Your_House_MovementScript_ToMom{
    walk_down * 3
    walk_left * 2
}

text GloriaTown_Your_House_TextScript_MomDialogueOne{
    format("Mom: The Professor came by this morning and told me that he wants you to stop by and see him to get your Starter and pokedex. Please be safe doing so and I love you {PLAYER}{KUN}. Also, make sure to see your friend Red beforehand like you told him!")
}
