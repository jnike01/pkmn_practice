mapscripts TestTown_Professors_Lab_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: TestTown_Professors_Lab_DefaultScript_Ball
}

script TestTown_Professors_Lab_DefaultScript_Ball{
    setvar(VAR_STARTER_REGION, 0)
}

script TestTown_Professors_Lab_TriggerScript_StarterChoose{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, TestTown_Professors_Lab_MovementScript_MeetProf)
    waitmovement(0)
    applymovement(4, TestTown_Professors_Lab_MovementScript_ProfMove)
    waitmovement(0)
    msgbox(TestTown_Professors_Lab_TextScript_BeginAdventure)
    waitmessage
    closemessage
    applymovement(4, TestTown_Professors_Lab_MovementScript_ProfMove2)
    waitmovement(0)
    setvar(VAR_TEST_TOWN_STATE, 1)
    release
}

script TestTown_Professors_Lab_TriggerScript_StarterChoose2{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, TestTown_Professors_Lab_MovementScript_MeetProf2)
    waitmovement(0)
    goto(TestTown_Professors_Lab_TriggerScript_StarterChoose)
}

movement TestTown_Professors_Lab_MovementScript_MeetProf{
    walk_up * 4
}

movement TestTown_Professors_Lab_MovementScript_MeetProf2{
    walk_left
}

movement TestTown_Professors_Lab_MovementScript_ProfMove{
    walk_right
    walk_down * 3
}

movement TestTown_Professors_Lab_MovementScript_ProfMove2{
    walk_up * 3
    walk_left
    face_down
}

text TestTown_Professors_Lab_TextScript_BeginAdventure{
    "Welcome to my lab!\p"
    "Thanks for stopping by and\n"
    "participating in our annual Pokemon\l"
    "giveaway for new Trainers.\p"
    "If you want more options\n" 
    "come let me know!\n"
}

script TestTown_Professors_Lab_TriggerScript_StopLeaving{
    lockall
    msgbox("You need to choose a Pokemon.")
    applymovement(OBJ_EVENT_ID_PLAYER, TestTown_Professors_Lab_MovementScript_DoNotLeave)
    waitmovement(0)
    releaseall
}

movement TestTown_Professors_Lab_MovementScript_DoNotLeave{
    walk_up
}

script TestTown_Professors_Lab_PersonScript_Aide1{
    lock
    faceplayer
    msgbox("The Professor has assigned me\nthe task of researching ancient\lhistory. There are so many wonders and\lmuch to learn!")
    release
    end
}

script TestTown_Professors_Lab_PersonScript_Aide2{
    lock 
    faceplayer
    msgbox("I'm so lucky to have been hired\nby the Professsor to help him in\lhis research!")
    release 
    end
}

script TestTown_Professors_Lab_PersonScript_Professor{
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        lock
        faceplayer
        message("Would you like to see more?")
        multichoice(25, 5, MULTI_STARTER_CHOICE, 0)
        closemessage
        if(var(VAR_RESULT) == 0){
            call(TestTown_Professors_Lab_FadeScript_StarterFade)
            setvar(VAR_STARTER_REGION, 2)
        }
        elif(var(VAR_RESULT) == 1){
            call(TestTown_Professors_Lab_FadeScript_StarterFade)
            setvar(VAR_STARTER_REGION, 1)
        }
        elif(var(VAR_RESULT) == 2){
            call(TestTown_Professors_Lab_FadeScript_StarterFade)
            setvar(VAR_STARTER_REGION, 0)
        }
        msgbox("Choose wisely...")
        waitmessage
        release
    } 
    else{
        msgbox("Take care of your Pokemon.", MSGBOX_NPC)
    }   
    end
}

script TestTown_Professors_Lab_FadeScript_StarterFade{
    fadescreen(FADE_TO_BLACK)
    delay(20)
    fadescreen(FADE_FROM_BLACK)
    return
}

script TestTown_Professors_Lab_ObjectScript_PokeballOne{
    lockall
    setvar(VAR_TEMP_0, 1)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_STARTER_REGION) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_MUDKIP)
            setvar(VAR_STARTER_CHOICE, SPECIES_MUDKIP)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
        elif(var(VAR_STARTER_REGION) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_TOTODILE)
            setvar(VAR_STARTER_CHOICE, SPECIES_TOTODILE)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
        elif(var(VAR_STARTER_REGION) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_SQUIRTLE)
            setvar(VAR_STARTER_CHOICE, SPECIES_SQUIRTLE)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't take another!")
    }
    waitmessage
    releaseall
    end
}

script TestTown_Professors_Lab_ObjectScript_PokeballTwo{
    lockall
    setvar(VAR_TEMP_0, 2)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_STARTER_REGION) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_TREECKO)
            setvar(VAR_STARTER_CHOICE, SPECIES_TREECKO)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
        elif(var(VAR_STARTER_REGION) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_CHIKORITA)
            setvar(VAR_STARTER_CHOICE, SPECIES_CHIKORITA)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
        elif(var(VAR_STARTER_REGION) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_BULBASAUR)
            setvar(VAR_STARTER_CHOICE, SPECIES_BULBASAUR)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't take another!")
    }
    waitmessage
    releaseall
    end
}

script TestTown_Professors_Lab_ObjectScript_PokeballThree{
    lockall
    setvar(VAR_TEMP_0, 3)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_STARTER_REGION) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_TORCHIC)
            setvar(VAR_STARTER_CHOICE, SPECIES_TORCHIC)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
        elif(var(VAR_STARTER_REGION) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_CYNDAQUIL)
            setvar(VAR_STARTER_CHOICE, SPECIES_CYNDAQUIL)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
        elif(var(VAR_STARTER_REGION) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_CHARMANDER)
            setvar(VAR_STARTER_CHOICE, SPECIES_CHARMANDER)
            call(TestTown_Professors_Lab_PokemonScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't take another!")
    }
    waitmessage
    releaseall
    end
}

script TestTown_Professors_Lab_PokemonScript_GiveStarter{
    showmonpic(VAR_STARTER_CHOICE, 10, 3)
    msgbox("Do you want to choose {STR_VAR_1}?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        setflag(FLAG_SYS_POKEMON_GET)
        setvar(VAR_TEST_TOWN_STATE, 2)
        givemon(VAR_STARTER_CHOICE, 5, ITEM_ORAN_BERRY)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("You have received {STR_VAR_1}.")
            removeobject(VAR_TEMP_0)
            call(TestTown_Professors_Lab_FlagScript_SetBallFlag)
            call(TestTown_Professors_Lab_SequenceScript_NicknamePartyMon)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("You have received {STR_VAR_1}.")
            removeobject(VAR_TEMP_0)
            call(TestTown_Professors_Lab_FlagScript_SetBallFlag)
            call(TestTown_Professors_Lab_SequenceScript_NicknameBoxMon)
            msgbox("Transferred {STR_VAR_1} to PC.")
        }
        else{
            hidemonpic
            msgbox("No more room for Pokemon.")
        }
    }
    else{
        hidemonpic
        msgbox(TestTown_Professors_Lab_TextScript_NotChosen)
    }
}

script TestTown_Professors_Lab_FlagScript_SetBallFlag{
    if(var(VAR_TEMP_0) == 1){
        setflag(FLAG_STARTER_BALL_ONE)
        return
    }
    if(var(VAR_TEMP_0) == 2){
        setflag(FLAG_STARTER_BALL_TWO)
        return
    }
    if(var(VAR_TEMP_0) == 3){
        setflag(FLAG_STARTER_BALL_THREE)
        return
    }
}

script TestTown_Professors_Lab_SequenceScript_HealMachine{
    lockall
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        msgbox("Please come back when you have\na Pokemon to heal.")
    }
    else{
        msgbox("Do you want to heal your Pokemon?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            special(HealPlayerParty)
            fadescreen(FADE_TO_BLACK)
            delay(30)
            fadescreen(FADE_FROM_BLACK)
            msgbox("Your Pokemon are now healthy!")
        }
        else{
            msgbox("Come back when you need healing.")
        }
    }
    releaseall
    end
}

script TestTown_Professors_Lab_SequenceScript_NicknamePartyMon{
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
}

script TestTown_Professors_Lab_SequenceScript_NicknameBoxMon{
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    call(Common_EventScript_TransferredToPC)
}

script TestTown_Professors_Lab_SignScript_HealMachine{
    lock
    msgbox("This is a complex machine. It might be\nused for healing?")
    release
    end
}

text TestTown_Professors_Lab_TextScript_NotChosen{
    "You did not choose the Pokemon"
}
