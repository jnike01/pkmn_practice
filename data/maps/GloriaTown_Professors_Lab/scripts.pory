mapscripts GloriaTown_Professors_Lab_MapScripts{}

script GloriaTown_Professors_Lab_TriggerScript_StarterChoose{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Professors_Lab_MovementScript_MeetProf)
    waitmovement(0)
    applymovement(4, GloriaTown_Professors_Lab_MovementScript_ProfMove)
    waitmovement(0)
    msgbox(GloriaTown_Professors_Lab_TextScript_BeginAdventure)
    waitmessage
    closemessage
    applymovement(4, GloriaTown_Professors_Lab_MovementScript_ProfMove2)
    waitmovement(0)
    setvar(VAR_GLORIA_TOWN_STATE, 1)
    release
}

script GloriaTown_Professors_Lab_TriggerScript_StarterChoose2{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Professors_Lab_MovementScript_MeetProf2)
    waitmovement(0)
    goto(GloriaTown_Professors_Lab_TriggerScript_StarterChoose)
}

movement GloriaTown_Professors_Lab_MovementScript_MeetProf{
    walk_up * 4
}

movement GloriaTown_Professors_Lab_MovementScript_MeetProf2{
    walk_left
}

movement GloriaTown_Professors_Lab_MovementScript_ProfMove{
    walk_right
    walk_down * 3
}

movement GloriaTown_Professors_Lab_MovementScript_ProfMove2{
    walk_up * 3
    walk_left
    face_down
}

text GloriaTown_Professors_Lab_TextScript_BeginAdventure{
    format("Welcome {PLAYER}{KUN}, to my lab! I'm glad you could come as I had invited you a bit abruptly. I hope you have an idea of what you want for a starter because I'm ready whenever you are! Just pick one of the three Pokeballs on the table by my desktop computer.")
}

script GloriaTown_Professors_Lab_TriggerScript_StopLeaving{
    lockall
    msgbox("You need to choose a Pokemon.")
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Professors_Lab_MovementScript_DoNotLeave)
    waitmovement(0)
    releaseall
}

script GloriaTown_Professors_Lab_TriggerScript_StopBeforeTable1{
    lockall
    msgbox("You need to choose a Pokemon.")
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Professors_Lab_MovementScript_StopBeforeTable1)
    waitmovement(0)
    releaseall
}

script GloriaTown_Professors_Lab_TriggerScript_StopBeforeTable2{
    lockall
    msgbox("You need to choose a Pokemon.")
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Professors_Lab_MovementScript_StopBeforeTable2)
    waitmovement(0)
    releaseall
}

movement GloriaTown_Professors_Lab_MovementScript_DoNotLeave{
    walk_up
}

movement GloriaTown_Professors_Lab_MovementScript_StopBeforeTable1{
    walk_right
}

movement GloriaTown_Professors_Lab_MovementScript_StopBeforeTable2{
    walk_down
}

script GloriaTown_Professors_Lab_PersonScript_Aide1{
    lock
    faceplayer
    msgbox(format("The Professor has assigned me the task of researching ancient history. There are so many wonders and much to learn!"))
    release
    end
}

script GloriaTown_Professors_Lab_PersonScript_Aide2{
    lock 
    faceplayer
    msgbox(format("I'm so lucky to have been hired by the Professsor to help him in his research!"))
    release 
    end
}

script GloriaTown_Professors_Lab_PersonScript_Professor{
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        lock
        faceplayer
        msgbox(format("Make sure to choose carefully since you have one chance at this. Make your choice count!"))
        release
    } 
    else{
        lock
        faceplayer
        msgbox(format("It makes me smile to see trainers receive their first Pokemon and go out to explore the world. Your always welcome to discuss anything regarding Pokemon or the region itself."))
        release
    }   
    end
}

script GloriaTown_Professors_Lab_ObjectScript_PokeballOne{
    lockall
    setvar(VAR_TEMP_0, 1)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        bufferspeciesname(STR_VAR_1, SPECIES_STARLY)
        setvar(VAR_STARTER_CHOICE, SPECIES_STARLY)
        call(GloriaTown_Professors_Lab_PokemonScript_GiveStarter)
    }
    else{
        msgbox("Don't take another!")
    }
    waitmessage
    releaseall
    end
}

script GloriaTown_Professors_Lab_ObjectScript_PokeballTwo{
    lockall
    setvar(VAR_TEMP_0, 2)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        bufferspeciesname(STR_VAR_1, SPECIES_ROLYCOLY)
        setvar(VAR_STARTER_CHOICE, SPECIES_ROLYCOLY)
        call(GloriaTown_Professors_Lab_PokemonScript_GiveStarter)
    }
    else{
        msgbox("Don't take another!")
    }
    waitmessage
    releaseall
    end
}

script GloriaTown_Professors_Lab_ObjectScript_PokeballThree{
    lockall
    setvar(VAR_TEMP_0, 3)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        bufferspeciesname(STR_VAR_1, SPECIES_TIMBURR)
        setvar(VAR_STARTER_CHOICE, SPECIES_TIMBURR)
        call(GloriaTown_Professors_Lab_PokemonScript_GiveStarter)
    }
    else{
        msgbox("Don't take another!")
    }
    waitmessage
    releaseall
    end
}

script GloriaTown_Professors_Lab_PokemonScript_GiveStarter{
    showmonpic(VAR_STARTER_CHOICE, 10, 3)
    msgbox("Do you want to choose {STR_VAR_1}?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        setflag(FLAG_SYS_POKEMON_GET)
        setvar(VAR_GLORIA_TOWN_STATE, 2)
        givemon(VAR_STARTER_CHOICE, 5, ITEM_ORAN_BERRY)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("You have received {STR_VAR_1}.")
            removeobject(VAR_TEMP_0)
            call(GloriaTown_Professors_Lab_FlagScript_SetBallFlag)
            call(GloriaTown_Professors_Lab_SequenceScript_NicknamePartyMon)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("You have received {STR_VAR_1}.")
            removeobject(VAR_TEMP_0)
            call(GloriaTown_Professors_Lab_FlagScript_SetBallFlag)
            call(GloriaTown_Professors_Lab_SequenceScript_NicknameBoxMon)
            msgbox("Transferred {STR_VAR_1} to PC.")
        }
        else{
            hidemonpic
            msgbox("No more room for Pokemon.")
        }
        closemessage
        call(GloriaTown_Professors_Lab_SequenceScript_InitiatePokedexSequence)
    }
    else{
        hidemonpic
        msgbox(GloriaTown_Professors_Lab_TextScript_NotChosen)
    }
}

script GloriaTown_Professors_Lab_FlagScript_SetBallFlag{
    if(var(VAR_TEMP_0) == 1){
        setflag(FLAG_STARTER_BALL_ONE)
        return
    }
    if(var(VAR_TEMP_0) == 2){
        setflag(FLAG_STARTER_BALL_TWO)
        return
    }
    if(var(VAR_TEMP_0) == 3){
        setflag(FLAG_STARTER_BALL_THREE)
        return
    }
}

script GloriaTown_Professors_Lab_SequenceScript_HealMachine{
    lockall
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        msgbox("Please come back when you have\na Pokemon to heal.")
    }
    else{
        msgbox("Do you want to heal your Pokemon?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            special(HealPlayerParty)
            fadescreen(FADE_TO_BLACK)
            playfanfare(MUS_HEAL)
            delay(120)
            fadescreen(FADE_FROM_BLACK)
            msgbox("Your Pokemon are now healthy!")
        }
        else{
            msgbox("Come back when you need healing.")
        }
    }
    releaseall
    end
}

script GloriaTown_Professors_Lab_SequenceScript_NicknamePartyMon{
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
}

script GloriaTown_Professors_Lab_SequenceScript_NicknameBoxMon{
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    call(Common_EventScript_TransferredToPC)
}

script GloriaTown_Professors_Lab_SignScript_HealMachine{
    lock
    msgbox("This is a complex machine. It might be\nused for healing?")
    release
    end
}

text GloriaTown_Professors_Lab_TextScript_NotChosen{
    "You did not choose your starter!"
}

script GloriaTown_Professors_Lab_SequenceScript_InitiatePokedexSequence{
    lock
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, GloriaTown_Professors_Lab_MovementScript_TurnToProf)
    if(var(VAR_TEMP_0) == 1){
        applymovement(4, GloriaTown_Professors_Lab_MovementScript_MoveBallOne)
    }
    if(var(VAR_TEMP_0) == 2){
        applymovement(4, GloriaTown_Professors_Lab_MovementScript_MoveBallTwo)
    }
    if(var(VAR_TEMP_0) == 3){
        applymovement(4, GloriaTown_Professors_Lab_MovementScript_MoveBallThree)
    }
    waitmovement(0)
    delay(20)
    call(GloriaTown_Professors_Lab_SequenceScript_GivePokedex)
    release
    return
}

script GloriaTown_Professors_Lab_SequenceScript_GivePokedex{
    lock
    msgbox(GloriaTown_Professors_Lab_TextScript_TakePokedex)
    playfanfare(MUS_OBTAIN_ITEM)
    message("{PLAYER}{KUN} received the Pokedex!$")
    waitfanfare
    closemessage
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_ADVENTURE_STARTED)
    special(SetUnlockedPokedexFlags)
    setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
    setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    setvar(VAR_PROFESSOR_STARTER_STATE, 1)
    setvar(VAR_RIVAL_STATE, 2)
    msgbox(GloriaTown_Professors_Lab_TextScript_ExplainPokedex)
    msgbox(GloriaTown_Professors_Lab_TextScript_ExplainPokedexUpgrade)
    call(GloriaTown_Professors_Lab_SequenceScript_UpgradeNationalDex)
    msgbox("Also, take these with you.")
    giveitem(ITEM_POKE_BALL, 5)
    msgbox(format("These allow you to catch your own Pokemon in tall grass so you can collect research for the Pokedex and create your ideal team for your journey."))
    waitmessage
    release
    end
}

script GloriaTown_Professors_Lab_SequenceScript_UpgradeNationalDex{
    lock
    playfanfare(MUS_OBTAIN_ITEM)
    message(GloriaTown_Professors_Lab_TextScript_PokedexUpgradedToNational)
    waitmessage
    waitfanfare
    setflag(FLAG_SYS_NATIONAL_DEX)
    special(EnableNationalPokedex)
    closemessage
    release
    return
}

movement GloriaTown_Professors_Lab_MovementScript_MoveBallOne{
    walk_left * 2
}

movement GloriaTown_Professors_Lab_MovementScript_MoveBallTwo{
    walk_left
}

movement GloriaTown_Professors_Lab_MovementScript_MoveBallThree{
    face_left
}

movement GloriaTown_Professors_Lab_MovementScript_TurnToProf{
    face_right
}

text GloriaTown_Professors_Lab_TextScript_ExplainPokedex{
    "Professor: The Pokedex is a high-tech\n"
	"tool that automatically makes a record\l"
	"of any Pokemon you meet or catch.\p"
}

text GloriaTown_Professors_Lab_TextScript_PokedexUpgradedToNational{
    "{PLAYER}'s Pokedex was upgraded\n"
	"to the National Mode!$"
}

text GloriaTown_Professors_Lab_TextScript_ExplainPokedexUpgrade{
    "Let me upgrade it as well..."
}

text GloriaTown_Professors_Lab_TextScript_TakePokedex{
    "Here, {PLAYER}, I ordered this for my\n"
	"research, but I think you should have\l"
	"this Pokedex."
}
